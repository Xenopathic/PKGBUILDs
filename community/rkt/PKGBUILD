# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

# Contributor: Yuval Adam <yuval at y3xz dot com> PGP-Key: 271386AA2EB7672F
# Contributor: Kenny Rasschaert <kenny dot rasschaert at gmail dot com> PGP-Key: 1F70454121E41419
# Contributor: Adrián Pérez de Castro <adrian at perezdecastro dor org> PGP-Key: 91C559DBE4C9123B
# Contributor: Carl George <arch at cgtx dot us> PGP-Key: 4BA2F7E101D9F512

pkgname=rkt
pkgver=1.21.0
pkgrel=1
pkgdesc="App container runtime"
arch=('x86_64')
url="https://github.com/coreos/rkt"
license=(apache)
depends=('glibc' 'openssl' 'zlib' 'systemd')
makedepends=('cpio' 'go' 'wget' 'squashfs-tools' 'perl-capture-tiny'
             'intltool' 'gperf' 'git' 'libseccomp' 'bc')

source=(https://github.com/coreos/rkt/archive/v$pkgver/$pkgname-$pkgver.tar.gz
        rkt.sysusers)
validpgpkeys=('04127D0BFABEC8871FFB2CCE50E0885593D2DCB4' '48F9B96A2E16137F')
sha256sums=('63eed8da954c24c1f9010e7513ee6ac21ffeaf63a648f9d5a0f9e9d3eb92fde5'
            '2aee4e8547843f4e6c032761b97cb723c1ecd384d508b86f44d16826bc34d6d6')
install="rkt.install"

build() {
  cd $pkgname-$pkgver
  export GOPATH="$PWD/Godeps/_workspace/src"

  ./autogen.sh
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --localstatedir=/var \
    --enable-tpm=auto \
    --with-stage1-flavors=host,fly \
    --with-stage1-default-flavor=host \
    --with-stage1-default-images-directory=/usr/lib/rkt/stage1-images \
    --with-stage1-default-location=/usr/lib/rkt/stage1-images/stage1-host.aci \

  make manpages
  make bash-completion
  make
}

package() {
  cd $pkgname-$pkgver/dist/init/systemd

  for unit in *.service *.timer *.socket; do
    install -Dm644 $unit "$pkgdir"/usr/lib/systemd/system/$unit
  done

  for tmpfile in tmpfiles.d/*.conf; do
    install -Dm644 $tmpfile "$pkgdir"/usr/lib/$tmpfile
  done

  cd "$srcdir"/$pkgname-$pkgver
  install -Dm644 "$srcdir"/rkt.sysusers "$pkgdir"/usr/lib/sysusers.d/rkt.conf
  install -Dm644 dist/bash_completion/rkt.bash "$pkgdir"/usr/share/bash-completion/completions/rkt

  cd dist/manpages
  for f in *; do
    install -Dm644 "$f" "$pkgdir/usr/share/man/man1/$f"
  done
  cd ../..

  cd build-$pkgname-$pkgver
  install -dm755 "$pkgdir"/usr/bin "$pkgdir"/usr/lib/rkt/stage1-images
  mv target/bin/rkt tools/actool "$pkgdir"/usr/bin
  mv target/bin/stage1-*.aci "$pkgdir"/usr/lib/rkt/stage1-images/
}

# vim:set ts=2 sw=2 et:
